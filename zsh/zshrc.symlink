# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# source /usr/lib/python3.5/site-packages/powerline/bindings/zsh/powerline.zsh

# Keep sensitive information like passwords off of Github.
source ~/.local.zsh

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# times

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# times

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# times

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# times

source /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

# If zsh-syntax-highlighting is in use, load it before this.
source /usr/share/zsh/plugins/zsh-history-substring-search/zsh-history-substring-search.zsh

unset config_files

# times
